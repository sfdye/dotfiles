# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U +X compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# Better history
# Credits to https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up
bindkey "^[[B" down-line-or-beginning-search # Down

# bat theme
export BAT_THEME="TwoDark"

# locale
export LC_ALL=en_US.UTF-8
export LANG=en_US.UTF-8

# brew init for Apple Silicon
if [[ `uname -m` == 'arm64' ]]; then
  eval $(/opt/homebrew/bin/brew shellenv)
fi

# pyenv
if command -v pyenv &> /dev/null; then

  # Add pyenv executable to PATH and
  # enable shims by adding the following
  # to ~/.profile and ~/.zprofile:

  export PYENV_ROOT="$HOME/.pyenv"
  export PATH="$PYENV_ROOT/bin:$PATH"
  eval "$(pyenv init --path)"

  eval "$(pyenv init -)"

  # pyenv-virtualenv
  unset PYENV_VIRTUALENV_DISABLE_PROMPT
  eval "$(pyenv virtualenv-init -)"

  # pyenv-virtualenvwrapper
  export WORKON_HOME=~/.pyenv/versions
  export PROJECT_HOME=$PROJECTS
  export PYENV_VIRTUALENVWRAPPER_PREFER_PYVENV="true"
  pyenv virtualenvwrapper_lazy
fi


# jenv
if command -v jenv &> /dev/null; then
  export PATH="$HOME/.jenv/bin:$PATH"
  eval "$(jenv init -)"
fi

# fzf & fd
export FZF_DEFAULT_COMMAND='fd --type file --color=always'
export FZF_DEFAULT_OPTS="--height=40% --layout=reverse --info=inline --ansi"
export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

if command -v atuin &> /dev/null; then
  eval "$(atuin init zsh)"
fi

# 1Password
export SSH_AUTH_SOCK=~/Library/Group\ Containers/2BUA8C4S2C.com.1password/t/agent.sock

export NVM_DIR="$HOME/.nvm"
[ -s "$NVM_DIR/nvm.sh" ] && \. "$NVM_DIR/nvm.sh"  # This loads nvm
[ -s "$NVM_DIR/bash_completion" ] && \. "$NVM_DIR/bash_completion"  # This loads nvm bash_completion

# GH Copilot
if command -v gh &> /dev/null; then
  eval "$(gh copilot alias -- zsh)"
fi

# Claude code
export PATH="$HOME/.local/bin:$PATH"

if command -v claude &> /dev/null; then
  alias ccd='claude --dangerously-skip-permissions'
fi

